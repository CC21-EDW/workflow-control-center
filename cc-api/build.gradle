/*
* Copyright 2021 Baloise Group
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
*     http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/

plugins {
    id 'org.springframework.boot' version '2.5.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'

    // to generate openapi.yml at build time
    id "com.github.johnrengelman.processes" version "0.5.0"
    id "org.springdoc.openapi-gradle-plugin" version "1.3.3"

    // sonar and test coverage
    id 'org.sonarqube'
    id 'jacoco'
}

group = 'com.baloise.open.edw'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

openApi {
    apiDocsUrl.set("http://localhost:8080/v3/api-docs")
    outputDir.set(file("../build/"))
    outputFileName.set("openapi.yml")
    waitTimeInSeconds.set(10)
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-ui:1.5.12'

    implementation 'org.apache.kafka:kafka-streams:3.0.0'
    implementation 'org.apache.kafka:kafka-clients:3.0.0'

    compileOnly 'org.projectlombok:lombok'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

jar {
    dependsOn "generateOpenApiDocs"
}

jacocoTestReport {
    reports {
        xml.enabled(true)
        html.enabled(true)
    }
}

test {
    useJUnitPlatform()
    finalizedBy('jacocoTestReport')
}
