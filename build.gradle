import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'org.springframework.boot' version '2.5.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'

    // to generate openapi.yml at build time
    id "com.github.johnrengelman.processes" version "0.5.0"
    id "org.springdoc.openapi-gradle-plugin" version "1.3.3"

    // sonar and test coverage
    id 'org.sonarqube' version '3.3'
    id 'jacoco'
}

group = 'com.baloise.open'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.organization", "cc21-edw"
        property "sonar.projectKey", "CC21-EDW_workflow-control-center"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.java.codeCoveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "$buildDir/reports/jacoco/test/jacoco.xml"
    }
}

openApi {
    apiDocsUrl.set("http://localhost:8080/v3/api-docs")
    outputDir.set(file("src/main/webapp/"))
    outputFileName.set("openapi.yml")
    waitTimeInSeconds.set(10)
}

jacocoTestReport {
    reports {
        xml.enabled(true)
        html.enabled(true)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-ui:1.5.12'

    compileOnly 'org.projectlombok:lombok'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
    finalizedBy('sonarqube')
}

String webappDir = "$projectDir${File.separator}src${File.separator}main${File.separator}webapp"
sourceSets {
    main {
        resources {
            srcDirs = ["$webappDir/dist/", "$projectDir/src/main/resources"]
        }
    }
}

jar {
    dependsOn "buildAngular"
}

String npmCmd = (Os.isFamily(Os.FAMILY_WINDOWS)) ? "npm.cmd" : "npm"

task buildAngular(type: Exec) {
    dependsOn "generateOpenApiDocs"
    dependsOn "installAngular"
    workingDir "$webappDir"
    inputs.dir "$webappDir"
    group = BasePlugin.BUILD_GROUP
    commandLine "$npmCmd", "run", "build"
}

task installAngular(type: Exec) {
    println "$npmCmd"
    workingDir "$webappDir"
    inputs.dir "$webappDir"
    group = BasePlugin.BUILD_GROUP
    commandLine "$npmCmd", "install"
}

tasks.named('sonarqube').configure {
    dependsOn jacocoTestReport
}
