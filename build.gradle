plugins {
    id 'org.springframework.boot' version '2.5.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'org.sonarqube' version '3.3'
    id 'jacoco'
}

group = 'com.baloise.open'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.organization", "cc21-edw"
        property "sonar.projectKey", "CC21-EDW_workflow-control-center"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.java.codeCoveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "$buildDir/reports/jacoco/test/jacocoTestReport.xml"
    }
}

jacocoTestReport {
    reports {
        xml.enabled(true)
        html.enabled(true)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator:2.5.5'
    implementation 'org.springframework.boot:spring-boot-starter-web:2.5.5'
    implementation 'org.springdoc:springdoc-openapi-ui:1.5.12'

    compileOnly 'org.projectlombok:lombok:1.18.22'

    developmentOnly 'org.springframework.boot:spring-boot-devtools:2.5.5'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor:2.5.5'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.5.5'
}

test {
    useJUnitPlatform()
//    finalizedBy('sonarqube')
}

String webappDir = "$projectDir/src/main/webapp"
sourceSets {
    main {
        resources {
            srcDirs = ["$webappDir/dist/", "$projectDir/src/main/resources"]
        }
    }
}

processResources {
    dependsOn "buildAngular"
}

task buildAngular(type: Exec) {
    dependsOn "installAngular"
    workingDir "$webappDir"
    inputs.dir "$webappDir"
    group = BasePlugin.BUILD_GROUP
    commandLine "npm", "run", "build"
}

task installAngular(type: Exec) {
    workingDir "$webappDir"
    inputs.dir "$webappDir"
    group = BasePlugin.BUILD_GROUP
    commandLine "npm", "install"
}

tasks.named('sonarqube').configure {
    dependsOn jacocoTestReport
}
